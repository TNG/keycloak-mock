import com.github.jengelman.gradle.plugins.shadow.transformers.ApacheNoticeResourceTransformer

plugins {
    id 'application'
    id 'com.github.gmazzo.buildconfig' version '3.0.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply plugin: 'com.google.cloud.tools.jib'

description = 'Standalone keycloak-mock server for use in frontend development'

shadowJar {
    classifier null
    manifest {
        attributes 'Main-Class': 'com.tngtech.keycloakmock.standalone.Main'
    }
    exclude 'META-INF/LICENSE.*'
    transform(ApacheNoticeResourceTransformer.class) {
        addHeader = true
        encoding = 'UTF-8'
        inceptionYear = 2019
        organizationName = company_name
        organizationURL = company_url
        projectName = 'Keycloak Mock Standalone'
    }
    metaInf {
        from "${rootDir}/NOTICE", "${rootDir}/LICENSE"
    }
}

task fakeJar(type: Jar) {
    from file("${project.projectDir}/src/main/resources/README.md")
    classifier 'fake'
}

publishing {
    publications {
        shadow(MavenPublication) {
            publication -> project.shadow.component(publication)
            artifact(fakeJar) {
                classifier 'javadoc'
            }
            artifact(fakeJar) {
                classifier 'sources'
            }
        }
    }
}

signing {
    sign publishing.publications.shadow
}

configurations {
    resourceJars
    all*.exclude group: 'javax.servlet'
}

dependencies {
    implementation project(':mock')
    implementation "info.picocli:picocli:$picocli_version"
    implementation "org.slf4j:slf4j-simple:$slf4j_version"
}

application {
    mainClass = 'com.tngtech.keycloakmock.standalone.Main'
}

shadow {
    mainClassName = application.mainClass.get()
}

buildConfig {
    buildConfigField('String', 'NAME', "\"${project.name}\"")
    buildConfigField('String', 'VERSION', "\"${project.version}\"")
    buildConfigField('long', 'BUILD_TIME', "${System.currentTimeMillis()}L")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    beforeTest { descriptor ->
        logger.lifecycle("Running test: ${descriptor}")
    }
}

jib {
    from {
        image = 'openjdk:11-jre-slim'
    }
    to {
        image = 'keycloak-mock/standalone'
    }
    container {
        ports = ['8000']
        mainClass = 'com.tngtech.keycloakmock.standalone.Main'
    }
}
