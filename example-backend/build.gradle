buildscript {
    ext {
        springBootVersion = '3.3.1'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'com.google.cloud.tools.jib'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'

ext['junit-jupiter.version'] = junit5_version

dependencies {
    // Using multiple engines in a single project may lead to different JUnit
    // Platform artifact versions being pulled in via transitive dependencies.
    // Gradle allows you to enforce usage of fixed set of versions that match.
    // https://docs.gradle.org/current/userguide/managing_transitive_dependencies.html#sec:bom_import
    implementation(enforcedPlatform("org.junit:junit-bom:$junit5_version")) {
        because 'enforce matching Platform, Jupiter, and Vintage versions'
    }

    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-security:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"
    implementation "org.keycloak:keycloak-policy-enforcer:$keycloak_version"
    testImplementation project(':mock-junit')
    testImplementation project(':mock-junit5')
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation "io.rest-assured:rest-assured:$restassured_version"
    testImplementation "io.rest-assured:json-path:$restassured_version"
    testImplementation "io.rest-assured:xml-path:$restassured_version"
    testImplementation "junit:junit:$junit4_version"
    testImplementation "org.junit.jupiter:junit-jupiter:$junit5_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5_version"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junit5_version"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    beforeTest { descriptor ->
        logger.lifecycle("Running test: ${descriptor}")
    }
}

jib {
    from {
        image = 'eclipse-temurin:21-jre'
    }
    to {
        image = 'keycloak-mock/example-backend'
    }
    container {
        ports = ['8080']
    }
}

tasks.register('prepareFrontend', Copy) {
    dependsOn ':example-frontend-react:yarn_build'
    from '../example-frontend-react/build/'
    into 'src/main/resources/static/'
}

processResources.dependsOn prepareFrontend
