buildscript {
    ext {
        springBootVersion = '2.1.7.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

// for some reason, spring boot pulls in junit5 in an outdated version, so we need to update it
// see https://stackoverflow.com/questions/54598484/gradle-5-junit-bom-and-spring-boot-incorrect-versions
ext['junit-jupiter.version'] = junit5_version

dependencies {
    def keycloak_version = '6.0.1'

    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-security:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"
    implementation "org.keycloak:keycloak-spring-boot-starter:$keycloak_version"
    testImplementation project(':mock-junit5')
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation "io.rest-assured:rest-assured:$restassured_version"
    testImplementation "io.rest-assured:json-path:$restassured_version"
    testImplementation "io.rest-assured:xml-path:$restassured_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit5_version"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    beforeTest { descriptor ->
        logger.lifecycle("Running test: ${descriptor}")
    }
}

compileJava.dependsOn 'googleJavaFormat'
