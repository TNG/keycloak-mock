import static groovy.io.FileType.FILES

plugins {
    id 'com.github.node-gradle.node' version '3.0.1'
}

node {
    // Version of node to use.
    version = '14.15.4'

    // Version of Yarn to use.
    yarnVersion = '1.22.10'

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory for unpacking node
    workDir = file('bin/nodejs')

    // Set the work directory for Yarn
    yarnWorkDir = file('bin/yarn')

    // Set the work directory where node_modules should be located
    nodeProjectDir = file("${project.projectDir}")
}

yarn_build {
    dependsOn yarn_install
    def files = []
    projectDir.traverse(type: FILES, nameFilter: ~/.*\.js(on){0,1}$/) {
        files << it
    }
    inputs.files(fileTree("$projectDir/src"), fileTree("$projectDir/public"), fileTree("$projectDir/tests"), files)
    outputs.dir("$projectDir/dist")
}

task copyNode(type: Copy) {
    if (org.gradle.internal.os.OperatingSystem.current().isLinux()) {
        from "${node.workDir}/node-v${node.version}-linux-x64/"
        into "${node.workDir}/node/"
    } else if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
        from "${node.workDir}/node-v${node.version}-darwin-x64/"
        into "${node.workDir}/node/"
    } else if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        from "${node.workDir}/node-v${node.version}-win-x64/"
        into "${node.workDir}/node/"
    }
}

task copyYarn(type: Copy) {
    from file("${node.yarnWorkDir}/yarn-v${node.yarnVersion}/")
    into file("${node.yarnWorkDir}/yarn/")
}

yarnSetup.finalizedBy(copyYarn)
nodeSetup.finalizedBy(copyNode)

task build {
    dependsOn yarn_build
}

yarn_e2e {
    mustRunAfter ':example-integration-docker:composeUp'
}
