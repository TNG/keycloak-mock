import static groovy.io.FileType.FILES

plugins {
    id 'com.github.node-gradle.node' version '7.1.0'
}

def nodeDir = 'bin/nodejs'
def yarnDir = 'bin/yarn'
def nodeVersionString = '24.12.0'

node {
    // Version of node to use.
    version = nodeVersionString

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory for unpacking node
    workDir = file(nodeDir)

    // Set the work directory for Yarn
    yarnWorkDir = file(yarnDir)

    // Set the work directory where node_modules should be located
    nodeProjectDir = file("${project.projectDir}")
}

yarn_build {
    dependsOn yarn_install
    def files = []
    projectDir.traverse(type: FILES, nameFilter: ~/.*\.js(on){0,1}$/) {
        files << it
    }
    inputs.files(fileTree("$projectDir/src"), fileTree("$projectDir/public"), fileTree("$projectDir/tests"), files)
    outputs.dir("$projectDir/dist")
}

tasks.register('copyNode', Copy) {
    dependsOn nodeSetup
    if (org.gradle.internal.os.OperatingSystem.current().isLinux()) {
        from "${projectDir}/${nodeDir}/node-v${nodeVersionString}-linux-x64/"
        into "${projectDir}/${nodeDir}/node/"
    } else if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
        from "${projectDir}/${nodeDir}/node-v${nodeVersionString}-darwin-x64/"
        into "${projectDir}/${nodeDir}/node/"
    } else if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        from "${projectDir}/${nodeDir}/node-v${nodeVersionString}-win-x64/"
        into "${projectDir}/${nodeDir}/node/"
    }
}

yarnSetup.dependsOn copyNode

tasks.register('build') {
    dependsOn yarn_build
}

yarn_e2e {
    mustRunAfter ':example-integration-docker:composeUp'
}
