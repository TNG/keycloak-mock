buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.jk1:gradle-license-report:1.13'
        classpath 'gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.8'
        classpath 'net.researchgate:gradle-release:2.8.1'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'pl.allegro.tech.build.axion-release' version '1.11.0'
}

wrapper {
    gradleVersion = '6.3'
    distributionType = Wrapper.DistributionType.ALL
}

scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
    branchVersionIncrementer = [
        'feature/.*' : 'incrementMinor',
        'master' : 'incrementMinor',
    ]
}

allprojects {
    apply plugin: 'com.github.sherter.google-java-format'

    repositories {
        mavenCentral()
    }

    group = 'com.tngtech.keycloakmock'
    version = scmVersion.version
}

ext {
    assertj_version = '3.15.0'
    jjwt_version = '0.11.1'
    junit4_version = '4.13'
    junit5_version = '5.6.1'
    picocli_version = '4.2.0'
    // do not update to 4.3.0 (see https://github.com/rest-assured/rest-assured/issues/1283)
    restassured_version = '4.2.0'
    vertx_version = '4.0.0-milestone4'

    license_name = 'The Apache License, Version 2.0'
    license_url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

    company_name = 'TNG Technology Consulting GmbH'
    company_url = 'https://www.tngtech.com'

    release_projects = ['mock', 'mock-junit', 'mock-junit5', 'standalone']
}

subprojects {
    apply plugin: 'com.github.jk1.dependency-license-report'

    if (release_projects.contains(project.name)) {
        apply plugin: 'net.researchgate.release'

        if ('standalone'.equals(project.name)) {
            apply plugin: 'java'
        } else {
            apply plugin: 'java-library'
        }
        apply plugin: 'maven'
        apply plugin: 'maven-publish'
        apply plugin: 'signing'

        sourceCompatibility = '1.8'

        if (!'standalone'.equals(project.name)) {
            task javadocJar(type: Jar) {
                from javadoc
                classifier 'javadoc'
                metaInf {
                    from "${rootDir}/NOTICE", "${rootDir}/LICENSE"
                }
            }
            task sourceJar(type: Jar) {
                from sourceSets.main.allSource
                classifier 'sources'
                metaInf {
                    from "${rootDir}/NOTICE", "${rootDir}/LICENSE"
                }
            }
            jar {
                metaInf {
                    from "${rootDir}/NOTICE", "${rootDir}/LICENSE"
                }
            }
        }

        dependencies {
            implementation 'com.google.code.findbugs:jsr305:3.0.2'
        }

        publishing {
            publications {
                if (!'standalone'.equals(project.name)) {
                    mavenJava(MavenPublication) {
                        from components.java
                        artifact javadocJar
                        artifact sourceJar
                    }
                }
            }
            repositories.maven {
                if (project.version.endsWith('-SNAPSHOT')) {
                    credentials.username project.findProperty('sonatypeUsername')
                    credentials.password project.findProperty('sonatypePassword')
                    url 'https://oss.sonatype.org/content/repositories/snapshots/'
                } else {
                    credentials.username project.findProperty('sonatypeUsername')
                    credentials.password project.findProperty('sonatypePassword')
                    url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                }
            }
        }
        afterReleaseBuild.dependsOn publish

        signing {
            required {
                gradle.taskGraph.hasTask('uploadArchives')
            }
            if (!'standalone'.equals(project.name)) {
                sign publishing.publications.mavenJava
            }
        }

        tasks.withType(Sign) {
            onlyIf { project.hasProperty('sonatypeUsername') }
        }

        afterEvaluate {
            publishing.publications*.pom {
                licenses {
                    license {
                        name = license_name
                        url = license_url
                    }
                }
                name = "${project.group}:${project.name}"
                url = 'https://github.com/TNG/keycloak-mock'
                description = "${project.description}"


                organization {
                    name = company_name
                    url = company_url
                }

                scm {
                    url = 'https://github.com/TNG/keycloak-mock'
                    connection = 'scm:git:git://github.com/TNG/keycloak-mock'
                    developerConnection = 'scm:git:ssh://github.com/TNG/keycloak-mock'
                }

                developers {
                    developer {
                        id = 'ostrya'
                        name = 'Kai Helbig'
                        email = 'kai.helbig@tngtech.com'
                    }
                    developer {
                        id = 'ripssi'
                        name = 'Simon Rips'
                        email = 'simon.rips@tngtech.com'
                    }
                }
            }
        }
    }
}
